// IVSmokeCompositePS.usf
#include "/Engine/Public/Platform.ush"
#include "/Engine/Private/Common.ush"
#include "/Engine/Private/ScreenPass.ush"

Texture2D SceneTex;
Texture2D SmokeAlbedoTex;
Texture2D SmokeLocalPosAlphaTex;
SamplerState LinearClamp_Sampler;
float Sharpness;
float2 ViewportSize;
float2 ViewRectMin;
float LowOpacityRemapThreshold;

void MainPS(
	float4 SvPosition : SV_POSITION,
	out float4 OutColor : SV_Target0)
{
	// UV for our smoke textures (0~1 mapping to TexSize)
	float2 SmokeUV = (SvPosition.xy - ViewRectMin) / ViewportSize;

	// Get SmokeAlbedoTex dimensions for filtering
	// Note: Smoke textures are upscaled from 1/2 resolution, so grain patterns
	// are 2x2 pixels. We use 2x TexelSize for blur to cover the expanded grain.
	uint width, height;
	SmokeAlbedoTex.GetDimensions(width, height);
	float2 TexelSize = 2.0f / float2(width, height);  // 2x for upscaled grain coverage
	
	uint sceneWidth, sceneHeight;
	SceneTex.GetDimensions(sceneWidth, sceneHeight);
	float2 SceneUV = SvPosition.xy / float2(sceneWidth, sceneHeight);
	
	// Sample textures
	float4 col = SceneTex.Sample(LinearClamp_Sampler, SceneUV);
	float4 smokeAlbedo = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV);
	float smokeMaskCenter = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV).a;

	float4 filteredSmoke;
	float filteredMask;

	if (Sharpness >= 0.0)
	{
		// Laplacian sharpening
		float4 albedo_n = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(0, 1));
		float4 albedo_e = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, 0));
		float4 albedo_s = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(0, -1));
		float4 albedo_w = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, 0));

		float neighbor = Sharpness * -1.0f;
		float center = Sharpness * 4.0f + 1.0f;
		filteredSmoke = albedo_n * neighbor + albedo_e * neighbor + smokeAlbedo * center + albedo_s * neighbor + albedo_w * neighbor;
		filteredMask = smokeMaskCenter;
	}
	else
	{
		// 3x3 Gaussian-like blur with 8 neighbors + center
		// Wider coverage for upscaled grain patterns
		float4 a0 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, -1));
		float4 a1 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(0, -1));
		float4 a2 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, -1));
		float4 a3 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, 0));
		float4 a4 = smokeAlbedo;  // center
		float4 a5 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, 0));
		float4 a6 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, 1));
		float4 a7 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(0, 1));
		float4 a8 = SmokeAlbedoTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, 1));

		float m0 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, -1)).a;
		float m1 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(0, -1)).a;
		float m2 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, -1)).a;
		float m3 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, 0)).a;
		float m4 = smokeMaskCenter;  // center
		float m5 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, 0)).a;
		float m6 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(-1, 1)).a;
		float m7 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(0, 1)).a;
		float m8 = SmokeLocalPosAlphaTex.Sample(LinearClamp_Sampler, SmokeUV + TexelSize * float2(1, 1)).a;

		// Gaussian weights (approximate): corners=1, edges=2, center=4, total=16
		float4 blurredAlbedo = (a0 + a2 + a6 + a8) + (a1 + a3 + a5 + a7) * 2.0 + a4 * 4.0;
		blurredAlbedo /= 16.0;

		float blurredMask = (m0 + m2 + m6 + m8) + (m1 + m3 + m5 + m7) * 2.0 + m4 * 4.0;
		blurredMask /= 16.0;

		float blurAmount = saturate(-Sharpness);
		filteredSmoke = lerp(smokeAlbedo, blurredAlbedo, blurAmount);
		filteredMask = lerp(smokeMaskCenter, blurredMask, blurAmount);
	}
	
	float SmokeMask = 1 - saturate(filteredMask);
	
	if (SmokeMask < LowOpacityRemapThreshold)
	{
		SmokeMask = max(0.0, (SmokeMask - LowOpacityRemapThreshold * 0.5f) * 2.0);
	}

	SmokeMask = saturate(1 - SmokeMask);
	OutColor = float4(filteredSmoke.rgb, SmokeMask);
	//OutColor = col * SmokeMask + filteredSmoke;
}
